#!/usr/bin/gawk -f

BEGIN {
  dryrun = 0
  for (arg in ARGV) {
    if (ARGV[arg] == "list") {
      action = "list"
      delete ARGV[arg]
    }
    else if (ARGV[arg] ~ /^docs\//) {
      document = ARGV[arg]
      print "got document", document
      delete ARGV[arg]
    }
    else if (ARGV[arg] == "dryrun") {
      dryrun = 1
      delete ARGV[arg]
    }
  }
  if (!(curl in SYMTAB)) {
    curl = "curl -sSL"
  }
}

function download(url, outfile) {
  cmd = sprintf("%s \"%s\">\"%s\"", curl, url, outfile)
  print "Executing:", cmd
  if (!(dryrun)) {
    system(cmd)
  }
}

sub(/^## /, "") {
  c++ # set chapter number
  s=0 # reset section number
  chapter=sprintf("%02i_%s", c, tolower(gensub(/[ /]+/, "_", "g", $0)))
}

/^https/ {
  s++
  url = $0
  split(url, line, "/")
  section = sprintf("%02i_%s", s, line[length(line)-1])
  outfile = sprintf("%s_%s.html", chapter, section)
  if (action == "list") {
    print outfile
  }
  else if (document) {
    split(document, d, "/")
    if (d[length(d)] == outfile) {
      download(url, document)
    }
  }
}
